# Profile to establish tftpboot/kickstart defaults on a standard SIMP server
#
# @param kickstart_server_uri
#   The URI of the kickstart server, including the top-level ks directory.
#   By default, this uses HTTP and the host's *primary* IP address.
#
class site::tftpboot (
  Simplib::URI $kickstart_server_uri = "https://${facts['ipaddress']}/ks"
){
  include '::tftpboot'

  case $facts['os']['release']['major'] {
    '7': {
      tftpboot::linux_model { 'el7_x86_64':
        kernel => 'centos-7-x86_64/vmlinuz',
        initrd => 'centos-7-x86_64/initrd.img',
        ks     => "${kickstart_server_uri}/pupclient_x86_64.cfg",
        extra  => "inst.noverifyssl ksdevice=bootif\nipappend 2"
      }
    }
    '6': {
      tftpboot::linux_model { 'el6_x86_64':
        kernel => 'centos-6-x86_64/vmlinuz',
        initrd => 'centos-6-x86_64/initrd.img',
        ks     => "${kickstart_server_uri}/pupclient_x86_64.cfg",
        extra  => "noverifyssl ksdevice=bootif\nipappend 2"
      }
    }
    default: {
      warn("${facts['os']['release']['major']} not supported for tftpboot")
    }
  }


  tftpboot::assign_host { 'default': model => "el${facts['os']['release']['major']}_x86_64" }
}

